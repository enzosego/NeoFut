plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.devtools.ksp' version '1.8.10-1.0.9'
}

android {
    namespace 'com.ensegov.neofut'
    compileSdk 33

    defaultConfig {
        applicationId "com.ensegov.neofut"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

dependencies {

    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"

    // Jetpack Compose
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation "androidx.compose.ui:ui:"
    implementation "androidx.compose.ui:ui-tooling-preview:"
    implementation "androidx.compose.material3:material3:"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_compose_version"

    // Compose Destinations
    implementation "io.github.raamcosta.compose-destinations:core:$compose_destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_destinations_version"

    // Coil for Compose
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Ktor Client & Engine
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    // Ktor Serialization & Negotiation
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    // Ktor Logging
    implementation "io.ktor:ktor-client-logging:$ktor_version"

    // Koin for Dependency Injection
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-workmanager:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_compose_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // Kotlin Extension and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // Testing
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:"
    debugImplementation "androidx.compose.ui:ui-tooling:"
    debugImplementation "androidx.compose.ui:ui-test-manifest:"

    // Kotest
    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core:$kotest_version"
    androidTestImplementation "io.kotest:kotest-assertions-core:$kotest_version"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // KotlinX Coroutines Testing
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Ktor Mock
    testImplementation "io.ktor:ktor-client-mock:$ktor_version"
}